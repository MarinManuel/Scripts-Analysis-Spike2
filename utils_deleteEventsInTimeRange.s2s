var startTime := 0, endTime;
endTime := MaxTime();
var refChan% := 401, delChan% := 401;
var deltaTime := 5e-3, tempDelta;
var ok%, curPos;
var deleteMode% := 2;

'hide the current script
if view(App(3)).WindowVisible()>0 then 
    view(App(3)).WindowVisible(0);
endif

DlgCreate("Parameters");
DlgChan(1, "Reference channel", 2+4+8+16);
DlgChan(2, "Channels with events to delete", 2+4+8+16+4096+16384);
DlgReal(3, "Time frame", 0, 10000);
DlgText("Mode",0,4);
DlgList(4, 40, "Delete nearest to evt in range|Delete all round evt in Range|Delete first in range Evt to Evt+TimeFrame|Delete all from Evt to Evt+TimeFrame", 100, 0,4);
DlgXValue(5, "Start analysis at");
DlgXValue(6, "  End analysis at");
ok% := DlgShow(refChan%, delChan%, deltaTime, deleteMode%, startTime, endTime);
if ok% then
    curPos := startTime;
    curPos := NextTime(refChan%, curPos);
    while curPos>=0 and curPos<endTime do
        if deleteMode%=2 or deleteMode%=3 then
            tempDelta := curPos+deltaTime;
        else
            tempDelta := deltaTime;
        endif
        MemDeleteTime(delChan%,deleteMode%,curPos,tempDelta);
        curPos := NextTime(refChan%, curPos);
    wend
endif
